Function SSNPattern() As String
    SSNPattern = "(\b\d{3}[-. ]?\d{2}[-. ]?\d{4}\b)|Social|Social Security Number|Tax ID|ITIN|TIN"
End Function

Sub CheckAllOpenWorkbooksForSSN()
    Dim cell As Range
    Dim regEx As Object
    Dim resultsWs As Worksheet
    Dim nextRow As Long
    Dim wb As Workbook
    Dim ws As Worksheet
    Dim ssnResultsSheetName As String
    
    ssnResultsSheetName = "SSN Search Results"
    
    Set regEx = CreateObject("VBScript.RegExp")
    regEx.Global = True
    regEx.IgnoreCase = True
    regEx.Pattern = SSNPattern()
    
    Application.DisplayAlerts = False
    On Error Resume Next
    ThisWorkbook.Sheets(ssnResultsSheetName).Delete
    On Error GoTo 0
    Application.DisplayAlerts = True
    
    Set resultsWs = ThisWorkbook.Worksheets.Add
    resultsWs.Name = ssnResultsSheetName
    resultsWs.Cells(1, 1).Value = "Workbook"
    resultsWs.Cells(1, 2).Value = "Sheet"
    resultsWs.Cells(1, 3).Value = "Cell"
    resultsWs.Cells(1, 4).Value = "SSN"
    resultsWs.Cells(1, 5).Value = "SSN Word Search"
    nextRow = 2
    
    For Each wb In Application.Workbooks
        For Each ws In wb.Worksheets
            For Each cell In ws.UsedRange
                If Not IsEmpty(cell.Value) Then
                    If VarType(cell.Value) = vbString Then
                        If regEx.Test(cell.Value) Then
                            resultsWs.Cells(nextRow, 1).Value = wb.Name
                            resultsWs.Cells(nextRow, 2).Value = ws.Name
                            resultsWs.Cells(nextRow, 3).Value = cell.Address
                            resultsWs.Cells(nextRow, 4).Value = cell.Value
                            resultsWs.Cells(nextRow, 5).Value = cell.Value
                            nextRow = nextRow + 1
                        End If
                    End If
                End If
            Next cell
        Next ws
    Next wb
    
    resultsWs.Columns("A:E").AutoFit
    
    Set regEx = Nothing
    Set resultsWs = Nothing
End Sub
